buildscript {

    ext.kotlin_version = '1.0.2'
    ext.powermock_version = '1.6.5'

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.5.RELEASE")

    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'

jar {
    version = '0.1.0'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {

    // MySQL
    compile "mysql:mysql-connector-java"
    testCompile "mysql:mysql-connector-java"
    runtime "mysql:mysql-connector-java"

    // Other
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-security"
    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "org.springframework.security.oauth:spring-security-oauth2:2.0.9.RELEASE"
    compile("org.springframework:spring-web")
    compile 'org.springframework:spring-context-support:4.2.6.RELEASE'

    compile "io.springfox:springfox-swagger2:2.4.0"
    compile "io.springfox:springfox-swagger-ui:2.4.0"

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    compile 'com.google.guava:guava:19.0'
    compile 'com.google.maps:google-maps-services:0.1.12'
    compile "org.javamoney:moneta:1.0"

    compile 'commons-validator:commons-validator:1.5.0'
    compile 'com.auth0:java-jwt:2.1.0'
    compile 'com.github.grumlimited:geocalc:v0.5.1'
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.7.4"

    testCompile "org.powermock:powermock-module-junit4:$powermock_version"
    testCompile "org.powermock:powermock-api-mockito:$powermock_version"

    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile "org.mockito:mockito-core:1.10.19"
    testCompile "com.jayway.jsonpath:json-path:2.2.0"
    testCompile "org.springframework.boot:spring-boot-starter-test"
    testCompile "junit:junit"

}

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}

// See: http://stackoverflow.com/questions/26389679/running-spring-app-built-with-gradle-on-heroku
task stage(type: Copy, dependsOn: [clean, assemble]) {
    from jar.archivePath
    into project.rootDir
    rename {
        'app.jar'
    }
}

stage.mustRunAfter(clean)

clean << {
    project.file('app.jar').delete()
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}
