buildscript {

    ext.kotlin_version = '1.2.40'
    ext.powermock_version = '1.7.4'
    ext.spring_boot_version = '1.5.12.RELEASE'

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version")

    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'

/*
 * Makes all beans annotated with  @Component, @Async,
 * @Transactional, @Cacheable non-final
 */
apply plugin: "kotlin-spring"

jar {
    version = '0.1.0'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {

    // MySQL
    compile "mysql:mysql-connector-java"
    testCompile "mysql:mysql-connector-java"
    runtime "mysql:mysql-connector-java"

    compile "com.zaxxer:HikariCP:3.1.0"

    // Other
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-security"
    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "org.springframework.boot:spring-boot-starter-cache"

    compile "org.springframework.security.oauth:spring-security-oauth2:2.3.2.RELEASE"
    compile("org.springframework:spring-web")
    compile 'org.springframework:spring-context-support:5.0.5.RELEASE'

    compile "io.springfox:springfox-swagger2:2.4.0"
    compile "io.springfox:springfox-swagger-ui:2.4.0"

    compile group: 'org.jsoup', name: 'jsoup', version: '1.10.2'

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

//    compile 'com.google.guava:guava:19.0'
    compile 'com.google.maps:google-maps-services:0.1.12'
    compile "org.javamoney:moneta:1.2.1"

    compile 'commons-validator:commons-validator:1.5.0'
    compile 'com.github.grumlimited:geocalc:v0.5.1'
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.7.9.1"
    compile "org.flywaydb:flyway-core:5.0.7"

    compile "com.cloudinary:cloudinary-http44:1.17.0"

    testCompile "org.powermock:powermock-module-junit4:$powermock_version"
    testCompile "org.powermock:powermock-api-mockito:$powermock_version"

    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile "org.mockito:mockito-core:1.10.19"
    testCompile "com.jayway.jsonpath:json-path:2.4.0"
    testCompile "org.springframework.boot:spring-boot-starter-test"
    testCompile "junit:junit"

}

task wrapper(type: Wrapper) {
    gradleVersion = '4.7'
}

// See: http://stackoverflow.com/questions/26389679/running-spring-app-built-with-gradle-on-heroku
task stage(type: Copy, dependsOn: [clean, assemble]) {
    from jar.archivePath
    into project.rootDir
    rename {
        'app.jar'
    }
}

stage.mustRunAfter(clean)

clean << {
    project.file('app.jar').delete()
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

test {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat = 'full'
    }
}
